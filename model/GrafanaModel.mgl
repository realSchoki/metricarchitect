import "model/FloorModel.mgl" as FloorModel

@style("model/General.style")
@primeviewer
graphModel GrafanaModel {
	package dev.schoki.metricarchitect.model.grafana
	nsURI "http://cinco.scce.info/product/metricarchitect/model/grafana"
	diagramExtension "grafana"
	
	attr EString as name
	
	@style(graphQuery, "${name}")
	@icon('src/main/resources/icons/chart-line-solid.png')
	node GraphQuery {
		@pvLabel (name)
     	@pvFileExtension ("floor")
     	prime FloorModel::DeviceGroup as deviceGroup
		outgoingEdges (GraphQueryPanel)
		attr EString as name := ""
	}
	
	@style(panel, "${name}")
	@icon('src/main/resources/icons/chalkboard-solid.png')
	node Panel {
		incomingEdges (GraphQueryPanel)
		outgoingEdges (PanelDashboard)
		attr EString as name := ""
	}
	
	@style(dashboard, "${name}")
	@icon('src/main/resources/icons/list-alt-regular.png')
	node Dashboard {
		incomingEdges (PanelDashboard)
		attr EString as name := ""
	}
	
	@style(simpleArrow)
	edge GraphQueryPanel {
		@possibleValuesProvider("dev.schoki.metricarchitect.provider.FilterValueProvider")
		attr EString as filter
		attr EString as function
		attr EString as offset
		attr EString as timespan
		attr EString as timeresolution
	}
	
	@style(simpleArrow)
	edge PanelDashboard {
	}
}

